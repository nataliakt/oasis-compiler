#Options
GenerateScanner = true
GenerateParser = true
Language = Java
ScannerName = Lexico
ParserName = Sintatico
SemanticName = Semantico
Package = br.nataliakelimthiel.recognizer.gals
ScannerCaseSensitive = true
ScannerTable = Full
Input = Stream
Parser = SLR
#RegularDefinitions
L : [a-zA-Z]
LMA : [A-Z]
LMI : [a-z]
D : [0-9]
WS : [\ \t\r]
NL : [\n]
AS : "'" [^ "'" ]* "'"
AD : \" [^ \" ]* \"
P : "."
U : "_"
CM : "#*" [^ "*" ]* "*#"
EN : "+" | "-"
AC: "{"
FC: "}"
DP: ":"
BI: "\"
#Tokens
MAID : {LMA} ({L} | {D} | {U})* // IDENTIFICADOR DE COMEÇANDO COM MAIUSCULO
MIID : {LMI} ({L} | {D} | {U})* // IDENTIFICADOR DE VARIÁVEL
ENCAP : {EN}? {L} ({L} | {D} | {U})* // ENCAPSULAMENTO

INTEIRO : {D}+ // NÚMERO INTEIRO
REAL : {D}+ {P}? {D}*
TEXTO : {AS} | {AD} // TEXTO

 : {BI} {NL}
 : {WS}* // IGNORA ESPAÇOS
 :! {CM} // IGNORA COMENTARIOS

NL : {NL}* // NOVA LINHA

// COMANDOS
MY = MIID : "my"

WRITE = MIID : "write"
READ = MIID : "read"

IF = MIID : "if"
ELSE = MIID : "else"

FOR = MIID : "for"

WHILE = MIID : "while"

INT = MIID : "integer"
DOUBLE = MIID : "decimal"
BOOLEAN = MIID : "bit"
STRING = MIID : "string"

TRUE = MIID : "true"
FALSE = MIID : "false"

MAIN = MIID : "main"

OU = MIID : "or"
E = MIID : "and"

// SÍMBOLOS
AC: {NL}* {AC}
FC: {FC} {NL}*
DEF : "="
DEFRET : ":="
AP : "("
FP : ")"
V : ","
P : "."
DP : {NL}* {DP} {NL}*

// COMPARAÇÃO
IGUAL : "=="
DIF : "^="
MAIOR : ">"
MENOR : "<"
MAIORIGUAL : ">="
MENORIGUAL : "<="
NEG : "^"

// OPERAÇÃO
SOMA : "+"
SUB : "-"
MULT : "*"
DIV : "/"
#NonTerminals
<BPRINCIPAL>
<CPRINCIPAL>

<CLASSE>
<CCLASSE>
<BCLASSE>

<ATRIBUTO>

<DEF>
<TIPO>
<BIT>
<OBJ>
<VALOR>
<VAL>

<BPARAM>
<CPARAM>

<EXP>
<EXP2>
<EXP3>
<EXP4>

<EXPL>
<EXPL2>
<EXPL3>
<EXPL4>
<EXPL5>
<EXPL6>
<EXPL7>
<EXPL8>
<EXPL9>
<EXPL10>

<CNVARIAVEL>
<BNVARIAVEL>

<NVARIAVEL>
<EVARIAVEL>

<UVARIAVEL>

<VAR>
<IDMETODO>
<METODO>
<ATRIBUTOS>
<BATRIBUTOS>
<THREAD>
<PRIV>
<RETORNO>

<COMANDO>
<BLOCO>

<IF>
<ELSE>

<FOR>

<WHILE>

<READ>
<WRITE>

<FUN>
#Grammar
<BPRINCIPAL> ::= <CPRINCIPAL> <BPRINCIPAL> | <CPRINCIPAL>;
<CPRINCIPAL> ::= <CLASSE>;

<CLASSE> ::= MAID AC <BCLASSE> FC;
<CCLASSE> ::= <ATRIBUTO> | <METODO> | NL;
<BCLASSE> ::= <CCLASSE> <BCLASSE> | î;

<ATRIBUTO> ::= <NVARIAVEL> NL;

<DEF> ::= DEF | DEFRET;
<TIPO> ::= INT | DOUBLE | BOOLEAN | STRING | MAID;
<BIT> ::= TRUE | FALSE;
<OBJ> ::= MAID AP  <BPARAM> FP | MY | MIID;
<VALOR> ::= <EXP>;
<VAL> ::= INTEIRO | REAL | <BIT> | TEXTO | <OBJ> | <FUN>;

<BPARAM> ::= <CPARAM> V <BPARAM> | <CPARAM> | î;
<CPARAM> ::= <VALOR>;

<EXP> ::= <EXP> SOMA <EXP2> | <EXP2>;
<EXP2> ::= <EXP2> SUB <EXP3> | <EXP3>;
<EXP3> ::= <EXP3> DIV <EXP4> | <EXP4>;
<EXP4> ::= <EXP4> MULT <EXPL> | <EXPL>;

<EXPL> ::= <EXPL> E <EXPL2> | <EXPL2>;
<EXPL2> ::= <EXPL2> OU <EXPL3> | <EXPL3>;
<EXPL3> ::= <EXPL3> DIF <EXPL4> | <EXPL4>;
<EXPL4> ::= <EXPL4> IGUAL <EXPL5> | <EXPL5>;
<EXPL5> ::= <EXPL5> MAIOR <EXPL6> | <EXPL6>;
<EXPL6> ::= <EXPL6> MENOR <EXPL7> | <EXPL7>;
<EXPL7> ::= <EXPL7> MAIORIGUAL <EXPL8> | <EXPL8>;
<EXPL8> ::= <EXPL8> MENORIGUAL <EXPL9> | <EXPL9>;
<EXPL9> ::= NEG <EXPL10> | <EXPL10>;
<EXPL10> ::= AP <EXP> FP | <VAL>;

<CNVARIAVEL> ::=  V <EVARIAVEL> | V MIID;
<BNVARIAVEL> ::= <CNVARIAVEL> <BNVARIAVEL> | î;

<NVARIAVEL> ::= <TIPO> MIID <DEF> <VALOR> <BNVARIAVEL> | <TIPO> MIID <BNVARIAVEL>;
<EVARIAVEL> ::= MIID <DEF> <VALOR>;

<UVARIAVEL> ::= <TIPO> <EVARIAVEL>;

<VAR> ::= MIID | MAID;
<IDMETODO> ::= MIID | ENCAP;
<METODO> ::= <THREAD> <IDMETODO> AP <ATRIBUTOS> FP <RETORNO> AC <BLOCO> FC;
<ATRIBUTOS> ::= <TIPO> MIID <DEF> <VALOR> <BATRIBUTOS> | <TIPO> MIID <BATRIBUTOS> | î;
<BATRIBUTOS> ::= V <ATRIBUTOS> | î;
<THREAD> ::= MAIN | î;
<RETORNO> ::= DP <TIPO> MIID <DEF> <VALOR> | DP <TIPO> MIID | î;

<COMANDO> ::= <NVARIAVEL> NL | <EVARIAVEL> NL | <IF> | <FOR> | <WHILE> | <WRITE> NL | <FUN> NL | NL;
<BLOCO> ::= <COMANDO> <BLOCO> | î;

<IF> ::= IF <VALOR> AC <BLOCO> FC <ELSE>;
<ELSE> ::= ELSE <VALOR> AC <BLOCO> FC <ELSE> | ELSE AC <BLOCO> FC | î;

<FOR> ::= FOR <UVARIAVEL> DP <VALOR> DP <EVARIAVEL> AC <BLOCO> FC;

<WHILE> ::= WHILE <VALOR> AC <BLOCO> FC;

<READ> ::= READ AP FP;
<WRITE> ::= WRITE AP <VALOR> FP;

<FUN> ::= <OBJ> P MIID AP <BPARAM> FP | MIID AP <BPARAM> FP | <READ>;
